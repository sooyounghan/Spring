-----
### QueryDSL 소개
-----
1. http://www.querydsl.com
2. 실무에서는 조건에 따라서 실행되는 쿼리가 달라지는 동적 쿼리를 많이 사용
3. 예) 주문 내역 검색으로 들어가보고, 이 예제를 Querydsl로 변경
4. build.gradle에 Querydsl 추가 (참고 : 스프링 DB / 13. QueryDSL / 2. QueryDSL 설정 참고 (+ 스프링 부트 3.0이상 버전 부분 참고))
  - 참고 : https://bit.ly/springboot3
```gradle
plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'jpabook'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-devtools'
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0'

	// Hibernate6Module
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate6'

	// 💡 Querydsl 추가 (스프링 부트 3.0 이상 : jakarta 주의)
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta' 
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta" 
	annotationProcessor "jakarta.annotation:jakarta.annotation-api" 
	annotationProcessor "jakarta.persistence:jakarta.persistence-api" 

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'

	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// JUnit4 추가
	testImplementation("org.junit.vintage:junit-vintage-engine") {
		exclude group: "org.hamcrest", module: "hamcrest-core"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

// 💡 Querydsl 추가, 자동 생성된 Q클래스 gradle clean으로 제거 (즉, Q 파일 생성을 위해 Clean)
// 💡💡💡 Q File은 컴파일 시점마다 생성되므로 git에 ignore에 제거
clean {
	delete file('src/main/generated')
}
```

  - 참고 : Maven
```gradle
//querydsl 추가
buildscript {
    dependencies {
        classpath("gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.10")
    }
}

plugins {
    id 'org.springframework.boot' version '2.1.9.RELEASE'
    id 'java'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: "com.ewerk.gradle.plugins.querydsl"

group = 'jpabook'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-devtools'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate5'
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.5.6'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    //querydsl 추가
    implementation 'com.querydsl:querydsl-jpa'

    //querydsl 추가
    implementation 'com.querydsl:querydsl-apt'
}

    //querydsl 추가
    //def querydslDir = 'src/main/generated'
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
    library = "com.querydsl:querydsl-apt"
    jpa = true
    querydslSourcesDir = querydslDir
}

sourceSets {  
    main {
        java {
            srcDirs = ['src/main/java', querydslDir]
        }
    }
}

compileQuerydsl{
    options.annotationProcessorPath = configurations.querydsl
}

configurations {
    querydsl.extendsFrom compileClasspath
}
```


6. Querydsl로 처리
```java
package jpabook.jpashop.repository;

import com.querydsl.core.types.dsl.BooleanExpression;
import com.querydsl.jpa.impl.JPAQueryFactory;
import jakarta.persistence.EntityManager;
import jakarta.persistence.TypedQuery;
import jakarta.persistence.criteria.*;
import jpabook.jpashop.domain.*;
import jpabook.jpashop.domain.Order;
import lombok.RequiredArgsConstructor;
import org.springframework.stereotype.Repository;
import org.springframework.util.StringUtils;

import java.util.ArrayList;
import java.util.List;

@Repository
public class OrderRepository {
    private final EntityManager em;
    private final JPAQueryFactory query

    public OrderRepository(EntityManager em) {
        this.em = em;
        this.query = new JPAQueryFactory(em);;
    }

    ...

    public List<Order> findAll(OrderSearch orderSearch) {
        QOrder order = QOrder.order; // static import 가능
        QMember member = QMember.member; // static import 가능

        return query.select(order)
                     .from(order)
                     .join(order.member, member)
                     .where(statusEq(orderSearch.getOrderStatus()), nameLike(orderSearch.getMemberName()))
                     .limit(1000)
                     .fetch();
    }

    private BooleanExpression nameLike(String memberName) {
        if(!StringUtils.hasText(memberName)) {
            return null;
        }

        return QMember.member.name.like("%" + memberName + "%"); // 💡 Hibernate 6 오류 (주의) : % + 검색어 + % 첨가 필요
    }

    private BooleanExpression statusEq(OrderStatus statusCond) {
        if(statusCond == null) {
            return null;
        }

        return QOrder.order.status.eq(statusCond);
    }
}
```

7. 정리
   - 💡 Queeydsl은 SQL(JPQL)과 모양이 유사하면서 자바 코드로 동적 쿼리 편리하게 생성 가능
   - 💡 실무에서는 복잡한 동적 쿼리를 많이 사용하게 되는데, 이 때 사용하면 높은 개발 생산성과 동시에 쿼리 오류를 컴파일 시점에 빠르게 잡을 수 있음
   - 💡 꼭 동적 쿼리가 아니라 정적 쿼리인 경우에도 다음과 같은 이유로 사용하는 것이 좋음
     + 직관적인 문법
     + 💡 컴파일 시점에 빠른 문법 오류 발견
     + 코드 자동 완성
     + 💡 코드 재사용 (JAVA)
     + 💡 JPQL new 명령어와는 비교가 안 될 정도로 깔끔한 DTO 조회 지원
  - 💡 Querydsl은 JPQL를 코드로 만드는 빌더 역할
    - 따라서, JPQL를 잘 이해하면 금방 습득 가능
    - 💡 JPA로 애플리케이션을 개발할 때 선택이 아닌 필수
