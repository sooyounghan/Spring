-----
### QueryDSL 설정
-----
1. 스프링 부트 2.X 설정
   - build.gradle
```gradle
plugins {
	id 'org.springframework.boot' version '2.6.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext["hibernate.version"] = "5.6.5.Final"

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	//JdbcTemplate 추가
	// implementation 'org.springframework.boot:spring-boot-starter-jdbc'

	//H2 데이터베이스 추가
	runtimeOnly 'com.h2database:h2'

	// MyBatis 스프링 부트 3.0 추가
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.3.1'

	//JPA, 스프링 데이터 JPA 추가
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	//Querydsl 추가
	implementation 'com.querydsl:querydsl-jpa'
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	//테스트에서 lombok 사용
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
	useJUnitPlatform()
}

//Querydsl 추가, 자동 생성된 Q클래스 gradle clean으로 제거
clean {
	delete file('src/main/generated')
}
```

  - QueryDSL로 추가된 부분은 두 부분
```gradle
dependencies {
	//Querydsl 추가
	implementation 'com.querydsl:querydsl-jpa'
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
}
```
```gradle
//Querydsl 추가, 자동 생성된 Q클래스 gradle clean으로 제거
clean {
	delete file('src/main/generated')
}
```

2. 스프링 부트 3.x 설정
```gradle
dependencies {
    //Querydsl 추가
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta' 
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta" 
    annotationProcessor "jakarta.annotation:jakarta.annotation-api" 
    annotationProcessor "jakarta.persistence:jakarta.persistence-api" 
}
```
  - 2.x와 비교하면 jpa가 jakarta로 변경
```gradle
implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta' 
annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta" 
```
```gradle
//Querydsl 추가, 자동 생성된 Q클래스 gradle clean으로 제거
clean {
	delete file('src/main/generated')
}
```

3. 검증 - Q타입 생성 확인 방법
   - Preferences → Build, Execution, Depolyment → Building Tools → Gradle
<div align="center">
<img src="https://github.com/user-attachments/assets/b0760188-7cfc-49c4-94fc-a2ecb8ba107d">
</div>

  - 위 2가지 옵션은 둘 다 같게 맞춤
  - Gradle : Gradle을 통해 빌드
  - IntelliJ IDEA : IntelliJ가 직접 자바를 실행해 빌드

 -----
 ### 옵션 선택 - Gradle - Q타입 생성 확인 방법
 -----
1. Gradle Intellij 사용법
  - Gradle → Tasks → build → clean
  - Gradle → Tasks → other → compile.java

2. Gradle 콘솔 사용법
  - ./gradlew clean compileJava

 3.Q타입 생성 확인
  - build → generated → sources → annotationProcessor → java/main 하위 hello.itemservice.domain.QItem이 생성되어 있어야함

4. 참고로 Q타입은 컴파일 시점에 자동 생성되므로 버전 관리(Git)에 포함되지 않는 것이 좋음
  - gradle 옵션을 선택하면 Q타입은 gradle/build 폴더 아래 생성되므로, 여기를 포함하지 않아야 함
  - 따라서, 대부분 gradle/build 폴더가 git에 포함하지 않기 때문에, 이 부분은 자연스럽게 해결

5. Q타입 삭제
   - gradle clean을 수행하면 build 폴더 자체 삭제

-----
### 옵션 선택 - IntelliJ IDEA - Q타입 생성 확인 방법
-----
1. Build → Build Project 또는 Build → Rebuild Project 또는 main(), 또는 테스트를 실행
2. src/main/generated 하위에  hello.itemservice.domain.QItem이 생성되어 있어야 함
3. 참고로 Q타입은 컴파일 시점에 자동 생성되므로 버전 관리(Git)에 포함되지 않는 것이 좋음
  - IntelliJ 옵션을 선택하면 Q타입은 src/main/generated 폴더 아래 생성되므로, 여기를 포함하지 않아야 함 (Git Commit 대상)

4. Q타입 삭제
```gradle
//Querydsl 추가, 자동 생성된 Q클래스 gradle clean으로 제거
clean {
	delete file('src/main/generated')
}
```
  - IntelliJ IDEA 옵션을 선택하면 src/main/generated에 파일이 생성되고, 필요한 경우 Q파일을 직접 삭제함
  - gradle에 해당 스크립트를 추가하면 gradle clean 명령어를 실행할 때, src/main/generated의 파일도 함께 삭제

-----
### 참고
-----
1. QueryDSL은 설정하는 부분이 까다로움
2. IntelliJ가 버전업 하거나 QueryDSL Gradle 설정이 버전업 하면서 적용 방법이 조금씩 달라지기도 함
3. 또한, 본인의 환경에 따라 잘 동작하지 않기도 함
4. 공식 메뉴얼에 querydsl gradle로 검색해서 확인
