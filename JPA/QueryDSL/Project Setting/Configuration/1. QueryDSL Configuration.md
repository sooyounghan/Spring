-----
### Querydsl 설정과 검증
-----
1. build.gradle 설정 추가
```gradle
plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.3'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'study'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Querydsl 추가 (스프링 부트 3.0 이상 : jakarta 주의)
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	
	// test 롬복 사용
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Querydsl 추가, 자동 생성된 Q클래스 gradle clean으로 제거 (즉, Q 파일 생성을 위해 Clean)
// Q File은 컴파일 시점마다 생성되므로 git에 ignore에 제거
clean {
	delete file('src/main/generated')
}
```

  - Querydsl 스프링 부트 3.0 관련 설정 방법 : https://bit.ly/springboot3

2. Gradle 전체 설정 - 스프링 부트 2.x
```gradle
plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    //querydsl 추가
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"     id 'java'
}

group = 'study'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8' configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'     implementation 'org.springframework.boot:spring-boot-starter-web'

    //querydsl 추가
    implementation 'com.querydsl:querydsl-jpa'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
         exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
     }  
}

test {
    useJUnitPlatform()
}

//querydsl 추가 시작
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}

sourceSets {
    main.java.srcDir querydslDir
}

configurations {
    querydsl.extendsFrom compileClasspath
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}
// querydsl 추가 끝
```

3. Querydsl 환경설정 검증
   - 검증용 엔티티 생성
```java
package study.querydsl.entity;

import jakarta.persistence.Entity;
import jakarta.persistence.GeneratedValue;
import jakarta.persistence.Id;
import lombok.Getter;
import lombok.Setter;

@Entity
@Getter @Setter
public class Hello {
    @Id @GeneratedValue
    private Long id;
}
```

  - 검증용 Q타입 생성
    + Gradle IntelliJ 사용법
      * Gradle → Tasks → build → clean
      * Gradle → Tasks → other → compileJava

    + Gradle 콘솔 사용법
      * ./gradlew clean compileJava

    + Q타입 생성 확인
      * build/generated/querydsl/QHello

    + study.querydsl.entity.QHello.java 파일이 생성되어 있어야 함
<div align="center">
<img src="https://github.com/user-attachments/assets/3d22fe55-1a88-436a-b0d7-1542172fc0df">
</div>

  - 💡 참고 : Q타입은 컴파일 시점에 자동 생성 되므로 버전 관리(Git)에 포함되지 않는 것이 좋음
    + 앞서 설정에서 생성 위치를 gradle/build 폴더 아래에 생성되도록 했기 때문에 이 부분도 자연스럽게 해결
    + 대부분 gradle/build 폴더를 git에 포함하지 않음

4. 테스트 케이스로 실행 검증
```java
package study.querydsl;

import com.querydsl.jpa.impl.JPAQueryFactory;
import jakarta.persistence.EntityManager;
import org.assertj.core.api.Assertions;
import org.junit.jupiter.api.Test;
import org.springframework.beans.factory.annotation.Autowired;
import org.springframework.boot.test.context.SpringBootTest;
import org.springframework.transaction.annotation.Transactional;
import study.querydsl.entity.Hello;
import study.querydsl.entity.QHello;

@SpringBootTest
@Transactional
class QuerydslApplicationTests {

	@Autowired EntityManager em;

	@Test
	void contextLoads() {
		Hello hello = new Hello();
		em.persist(hello);

		JPAQueryFactory query = new JPAQueryFactory(em);
		// QHello qHello = new QHello("h");
		QHello qHello = QHello.hello; // Querydsl Q타입 동작 확인

		Hello result = query.selectFrom(qHello)
				    .fetchOne();

		Assertions.assertThat(result).isEqualTo(hello);
		// Lombok 동작 확인 (hello.getId())
		Assertions.assertThat(result.getId()).isEqualTo(hello.getId());
	}

}
```

  - 참고 : 스프링 부트에 아무런 설정도 하지 않으면 H2 DB를 메모리 모드로 JVM 안에서 실행
